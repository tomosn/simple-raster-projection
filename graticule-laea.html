<!DOCTYPE HTML>
<!--
 Graticule Renderer (Azimuthal Equidistant Projection) v0.0.1  2019-02-10
    https://github.com/tomosn/simple-raster-projection
 Copyright (C) 2019 T.Seno
 All rights reserved.
 @license MIT License (https://raw.githubusercontent.com/tomosn/simple-raster-projection/master/LICENSE)
-->
<html lang="ja">
<head>
<title>Lambert Azimuthal Equal-Area Projection</title>
<meta charset="utf-8">
<script type="application/javascript" src="./js/graticule-proj-renderer.js"></script>
<script type="application/javascript" src="./lib/webgl-utils.js"></script>

<!-- vertex shader -->
<script id="shader-vs" type="x-shader/x-vertex">
  precision highp float;
  attribute float aCoordX;
  attribute float aCoordY;
  attribute vec2 aViewCoord;     //  投影後のView上のXY座標系での範囲に対応
  varying vec2 vViewCoord;

  void main() {
    gl_Position = vec4(aCoordX, aCoordY, 1.0, 1.0);
    vViewCoord = aViewCoord;
  }
</script>


<!-- fragment shader -->
<script id="shader-fs" type="x-shader/x-fragment">
precision highp float;
varying vec2 vViewCoord;    //  表示中のView上の座標値
uniform vec2 uProjCenter;   //  投影中心
uniform vec2 uViewLengthPerPixel;      // 画面1pixelあたりのXY座標系の長さ
uniform float uGraticuleIntervalDeg;   //  緯度経度線の描画間隔[degrees]

const float pi = 3.14159265;
const float epsilon = 0.00000001;
const float xyRadius = 2.0;

//   LAEA逆変換
vec2 proj_inverse(vec2 center, vec2 xy)
{
  float sinPhi0 = sin(center.y);
  float cosPhi0 = cos(center.y);

  float rho = length(xy);

  if ( rho < epsilon ) {
    return center;
  }
  if ( rho - epsilon > xyRadius ) {
    rho = xyRadius;
  }

  float c_rh = 2.0 * asin( clamp( rho/2.0, -1.0, 1.0) );

  float cos_c = cos(c_rh);
  float sin_c = sin(c_rh);

  float phi = asin( clamp( cos_c * sinPhi0 + xy.y * sin_c * cosPhi0 / rho, -1.0, 1.0 ) );
  float lam = mod( center.x + atan( xy.x * sin_c, rho * cosPhi0 * cos_c - xy.y * sinPhi0 * sin_c ) + pi, 2.0 * pi ) - pi;

  return vec2(lam, phi);
}

//  XY座標の値が範囲内か否か
float validate_xy(vec2 xy) {
  return 1.0 - step( xyRadius, length(xy) );
}

//  緯度経度線描画のための関数
vec2 graticule_level(vec2 lp, vec2 baseLonLat) {
  vec2 lonlat = degrees(lp);
  if (baseLonLat.x < -90.0 || 90.0 < baseLonLat.x) {
    lonlat.x = mod(lonlat.x + 360.0, 360.0);     //  連続性を保つため日付変更線付近では基準を変更
  }
  return floor(lonlat / uGraticuleIntervalDeg);
}

//   緯度経度線描画
bool render_graticule() {
  if ( validate_xy(vViewCoord) == 0.0 ) {
    return false;
  }

  vec2 lp = proj_inverse(uProjCenter, vViewCoord);      //  緯度経度[rad]
  vec2 baseLonLat = degrees(lp);      //  該当ピクセルの緯度経度[deg]
  float absLat = abs(baseLonLat.y);   //  単純に値を使用すると、gratical_level()の値の大きい方に線が偏ってしまうため絶対値を使用
  if (85.0 < absLat) {
    return false;   //  両極付近は描画対象外
  }

  //    0 1 2
  //    3 4 5
  //    6 7 8
  vec2 v1 = vViewCoord + vec2(0.0, uViewLengthPerPixel.y);    //  上側のピクセル
  vec2 v3 = vViewCoord + vec2(-uViewLengthPerPixel.x, 0.0);   //  左側のピクセル
  vec2 v5 = vViewCoord + vec2(uViewLengthPerPixel.x, 0.0);    //  右側のピクセル
  vec2 v7 = vViewCoord + vec2(0.0, -uViewLengthPerPixel.y);    //  下側のピクセル

  if ( validate_xy(v1) == 0.0 ||  validate_xy(v3) == 0.0 || validate_xy(v5) == 0.0 || validate_xy(v7) == 0.0) {
    return false;
  }

  vec2 z = -4.0 * graticule_level(lp, baseLonLat);
  z += graticule_level(proj_inverse(uProjCenter, v1), baseLonLat);
  z += graticule_level(proj_inverse(uProjCenter, v3), baseLonLat);
  z += graticule_level(proj_inverse(uProjCenter, v5), baseLonLat);
  z += graticule_level(proj_inverse(uProjCenter, v7), baseLonLat);

  vec2 c = min(abs(z) / 2.0, 1.0);
  float gray = 0.0;

  if (80.0 < absLat) {
    gray = c.y;    //  ±80度より極付近は経線は描画しない
  } else {
    gray = max(c.x, c.y);
  }

  if (gray == 0.0) {
    return false;
  }

  vec3 bgColor = vec3(0.0);
  vec3 lineColor = vec3(0.9);
  gl_FragColor = vec4( mix(bgColor, lineColor, gray), 1.0);

  return true;
}

void main() {
  bool rendered = render_graticule();
  if ( !rendered ) {
    discard;
  }
}
</script>


<!-- main -->
<script type="text/javascript">
  let gl;
  let canvas;
  let shaderProgram;

  let requestId;

  let canvasSize = null;

  let step = 0;


  function getShaderStr(id) {
      const elem = document.getElementById(id);
      if (!elem) {
        return null;
      }
      return elem.firstChild.textContent;
  }

  function draw() {
    requestId = requestAnimationFrame(draw);

    step += 1;

    //  投影中心の緯度経度
    //var lon0 = 130.0;
    const lon0 = step * 0.5;
    const lat0 = 40.0;
    const projCenter = [ lon0, lat0 ];

    //  Viewの表示範囲の指定
    const viewRect = [ -2.0, -2.0, 2.0, 2.0 ];

    //  投影中心の緯度経度とView上の表示範囲を指定する
    shaderProgram.prepareRender(projCenter, viewRect, canvasSize);
    shaderProgram.renderGraticule(10.0);
  }

  function startup() {
    canvas = document.getElementById("webglCanvas");
    gl = WebGLUtils.setupWebGL(canvas);
    if (!gl) {
      alert('Failed to setup WebGL.');
      return;
    }

    canvasSize = [ parseInt(canvas.width), parseInt(canvas.height) ];

    const vsStr = getShaderStr("shader-vs");
    const fsStr = getShaderStr("shader-fs");

    shaderProgram = new ShaderProgram(gl);
    shaderProgram.init(vsStr, fsStr);

    draw();
  }
</script>

</head>

<body onload="startup();">
  <canvas id="webglCanvas" width="512" height="512"></canvas>
</body>

</html>
