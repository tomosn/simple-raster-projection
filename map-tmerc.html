<!DOCTYPE HTML>
<!--
 Simple Raster Projection (Transverse Mercator Projection) v0.0.2  2019-02-10
    https://github.com/tomosn/simple-raster-projection
 Copyright (C) 2019 T.Seno
 All rights reserved.
 @license MIT License (https://raw.githubusercontent.com/tomosn/simple-raster-projection/master/LICENSE)
-->
<html lang="ja">
<head>
<title>Transverse Mercator Projection</title>
<meta charset="utf-8">
<script type="application/javascript" src="./js/raster-proj-renderer.js"></script>
<script type="application/javascript" src="./lib/webgl-utils.js"></script>

<!-- vertex shader -->
<script id="shader-vs" type="x-shader/x-vertex">
  precision highp float;
  attribute float aCoordX;
  attribute float aCoordY;
  attribute vec2 aViewCoord;     //  投影後のView上のXY座標系での範囲に対応
  varying vec2 vViewCoord;

  void main() {
    gl_Position = vec4(aCoordX, aCoordY, 1.0, 1.0);
    vViewCoord = aViewCoord;
  }
</script>


<!-- fragment shader -->
<script id="shader-fs" type="x-shader/x-fragment">
  precision highp float;
  varying vec2 vViewCoord;    //  表示中のView上の座標値
  uniform vec2 uDataCoord1;   //  画像データのSouthWestの座標値
  uniform vec2 uDataCoord2;   //  画像データのNorthEastの座標値
  uniform vec2 uProjCenter;   //  投影中心
  uniform vec2 uViewLengthPerPixel;    // 画面1pixelあたりのXY座標系の長さ
  uniform float uGraticuleIntervalDeg;   //  緯度経度線の描画間隔[degrees]
  uniform sampler2D uTexture;

  const float pi = 3.14159265;
  const float epsilon = 0.00000001;
  const float xyRadius = pi;

  //   TMERC逆変換
  vec2 proj_inverse(vec2 center, vec2 xy) {
    float d = xy.y + center.y;

    float ep = exp(xy.x);
    float em = exp(-xy.x);
    float ch = (ep + em) / 2.0;
    float sh = (ep - em) / 2.0;

    float phi = asin( clamp( sin(d) / ch, -1.0, 1.0 ) );
    float lam = mod( center.x + atan( sh, cos(d) ) + pi, 2.0 * pi ) - pi;

    return vec2(lam, phi);
  }

  //  XY座標の値が範囲内か否か
  float validate_xy(vec2 xy) {
    return 1.0;
  }

  //  緯度経度線描画のための関数
  vec2 graticule_level(vec2 lp, vec2 baseLonLat) {
    vec2 lonlat = degrees(lp);
    if ( 135.0 < abs(baseLonLat.x) ) {
      lonlat.x = mod(lonlat.x + 360.0, 360.0);     //  連続性を保つため日付変更線付近では基準を変更
    }
    return floor(lonlat / uGraticuleIntervalDeg);
  }

  //   緯度経度線描画
  bool render_graticule() {
    if ( validate_xy(vViewCoord) == 0.0 ) {
      return false;
    }

    vec2 lp = proj_inverse(uProjCenter, vViewCoord);                 //  緯度経度
    vec2 baseLonLat = degrees(lp);      //  該当ピクセルの緯度経度
    float absLat = abs(baseLonLat.y);
    if (81.0 < absLat) {
      return false;   //  両極付近は描画対象外
    }

    vec2 v1 = vViewCoord + vec2(0.0, uViewLengthPerPixel.y);
    vec2 v3 = vViewCoord + vec2(-uViewLengthPerPixel.x, 0.0);
    vec2 v5 = vViewCoord + vec2(uViewLengthPerPixel.x, 0.0);
    vec2 v7 = vViewCoord + vec2(0.0, -uViewLengthPerPixel.y);

    if ( validate_xy(v1) == 0.0 ||  validate_xy(v3) == 0.0 || validate_xy(v5) == 0.0 || validate_xy(v7) == 0.0) {
      return false;
    }

    vec2 z = -4.0 * graticule_level(lp, baseLonLat);
    z += graticule_level(proj_inverse(uProjCenter, v1), baseLonLat);
    z += graticule_level(proj_inverse(uProjCenter, v3), baseLonLat);
    z += graticule_level(proj_inverse(uProjCenter, v5), baseLonLat);
    z += graticule_level(proj_inverse(uProjCenter, v7), baseLonLat);

    vec2 col = min(abs(z) / 2.0, 1.0);
    float alpha = 0.0;
    if (80.0 < absLat) {
      alpha = col.y;    //  ±80度より極付近は経線は描画しない
    } else {
      alpha = max(col.x, col.y);
    }

    if (alpha == 0.0) {
      return false;
    }

    vec3 lineColor = vec3(0.8);
    gl_FragColor = vec4(lineColor, alpha * 0.4);

    return true;
  }

  //  テクスチャ描画
  bool render_surface() {
    if ( validate_xy(vViewCoord) == 0.0 ) {
      return false;
    }

    //   画像の描画
    vec2 lp = proj_inverse(uProjCenter, vViewCoord);                 //  緯度経度
    vec2 ts = (lp - uDataCoord1) / (uDataCoord2 - uDataCoord1);      //  変換元画像上の座標
    if ( 0.0 <= ts.x && 0.0 <= ts.y && ts.x <= 1.0 && ts.y <= 1.0) {
      gl_FragColor = texture2D(uTexture, ts);
      return true;
    } else {
      return false;
    }
  }

  void main() {
    bool rendered = false;
    if ( 0.0 < uGraticuleIntervalDeg ) {
      rendered = render_graticule();
    } else {
      rendered = render_surface();
    }
    if ( !rendered ) {
      discard;
    }
  }
</script>


<!-- main -->
<script type="text/javascript">
  let gl;
  let canvas;
  let shaderProgram;

  let requestId;

  let textures = [];
  let canavsSize = null;

  let step = 0;


  function getShaderStr(id) {
      const elem = document.getElementById(id);
      if (!elem) {
        return null;
      }
      return elem.firstChild.textContent;
  }

  function loadImage(url, dataRect) {
    const img = new Image();
    img.onload = function() {
      var tex = shaderProgram.createTexture(img);
      if ( tex ) {
        textures.push({ "textureId":tex, "dataRect":dataRect });
      }
    };
    img.src = url;
  }

  function draw() {
    requestId = requestAnimationFrame(draw);

    step += 1;

    //  投影中心の緯度経度
    //var lon0 = 130.0;
    const lon0 = step * 0.5;
    const lat0 = 40.0;
    const projCenter = [ lon0, lat0 ];

    //  Viewの表示範囲の指定
    const viewRect = [ -Math.PI, -Math.PI, +Math.PI, +Math.PI ];

    //  投影中心の緯度経度とView上の表示範囲を指定する
    shaderProgram.prepareRender(projCenter, viewRect, canvasSize);
    for (let i = 0; i < textures.length; i++) {
      const texture = textures[i];
      //  dataRectで指定された緯度経度の矩形に対応するtextureを描画する
      shaderProgram.renderSurface(texture.textureId, texture.dataRect);
    }
    shaderProgram.renderGraticule(20.0);
  }

  function startup() {
    canvas = document.getElementById("webglCanvas");
    gl = WebGLUtils.setupWebGL(canvas);
    if (!gl) {
      alert('Failed to setup WebGL.');
      return;
    }

    canvasSize = [ parseInt(canvas.width), parseInt(canvas.height) ];

    const vsStr = getShaderStr("shader-vs");
    const fsStr = getShaderStr("shader-fs");

    shaderProgram = new ShaderProgram(gl);
    shaderProgram.init(vsStr, fsStr);

    //  緯度経度座標系上の画像の読み込み
    //    第２引数は、画像の範囲（緯度経度の矩形）
    loadImage('./data/NE_180D_0_0.png', [-180.0, -90.0, 0.0, +90.0]);
    loadImage('./data/NE_180D_1_0.png', [0.0, -90.0, +180.0, +90.0]);

    draw();
  }
</script>

</head>

<body onload="startup();">
  <canvas id="webglCanvas" width="512" height="512"></canvas>
</body>

</html>
